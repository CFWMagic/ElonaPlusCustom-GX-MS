#deffunc atxinit
	if ( atxthrough == 0 ) {
		notesel atbuff
		atbuff = ""
	}
	atxthrough = 0
	listmax = 0
	cs = 0
	page = 0
	cc = CHARA_PLAYER
	cs_bk = -1
	pagesize = 16
	chatesc = -1
	if ( atxbg != atxbgbk ) {
		mode = MODELIST_MAIN
		screenupdate = -1
		gosub *screen_draw
		mode = MODELIST_TXT_ADV
		atxbgbk = atxbg
		gsel 4
		gmode 0
		pos 0, 0
		picload exedir + "\\graphic\\" + atxbg + ".bmp", 1
		pos 0, inf_msgh
		gzoom windoww, windowh - inf_verh - inf_msgh, 4, 0, 0, 240, 160
		gmode 2
		p = windoww / 192
		repeat p + 1
			if ( cnt == p ) {
				sx = windoww \ 192
			}
			else {
				sx = 192
			}
			pos cnt * 192, 0
			gcopy 3, 496, 528, sx, inf_msgh
		loop
		window2 windoww - 208, 0, 208, 98, 0, 0
		pos windoww - 204, 4
		gzoom 200, 90, 0, 120, 88, windoww - 120, windowh - inf_verh - 112, 1
		
		/********** JAMES CUSTOM - SLOTS - BEGINNING **********/
		gsel 7
		picload exedir + "graphic\\interface2.bmp"
		b@txtadv = 5,   5,   1,   5,   5,   1,   5,   1
		p@txtadv = 168, 596, 113, 211, 345, 971, 334, 561
		ys@txtadv =12,  8,   0,   16,  0,   0,   0,   0
		repeat 8
			gmode 2
			pos cnt * 48, 96
			gzoom 48, 48, 7, 0 + 72 * (1+cnt), 0, 72, 96 
			gcopy b@txtadv(cnt), p@txtadv(cnt) \ 33 * 48, p@txtadv(cnt) / 33 * 48 + ys@txtadv(cnt), 48, 48
			gcopy 3, 0, 240, 48, 48
		loop
        gmode 0
		/********** JAMES CUSTOM - SLOTS - ENDING **********/

		gsel 0
	}
	return

#deffunc begintempinv
	file = "shoptmp.s2"
	fmode = 4
	gosub *game_ctrlFile
	inv_getheader -1
	repeat invrange, invhead
		inv(INV_ITEM_NUM, cnt) = 0
	loop
	return

#deffunc exittempinv
	file = "shoptmp.s2"
	fmode = 3
	gosub *game_ctrlFile
	return

*com_txtadv
	begintempinv
	atxthrough = 0
	atxpic = 0
	snd SOUNDLIST_POP3
	mode = MODELIST_TXT_ADV
	atxbg = "bg13"
	atxbgbk = ""
	sdim atxinfon, 80, 5
	/********** JAMES CUSTOM - BEGINNING **********/ // Fix sleeping crashes caused by uninitiated atxinfon (type mismatch).
	atxinfon(0) = ""
	/********** JAMES CUSTOM - ENDING **********/ // Fix sleeping crashes
	atxci = inv(INV_ITEM_ID, limit(ci,0,MAX_INV_DATA-1))
	redraw 0
	if ( atxid == 2 ) {
		goto *atx_RE
	}
	if ( atxid == 1 ) {
		txt lang("ディーラーに話しかけた。", "You talk to the dealer.")
		music = MUSICLIST_MCCASINO
		gosub *music_play
		goto *adv_casinoCard
	}
	if ( atxid == 4 ) {
		music = MUSICLIST_MCCASINO
		gosub *music_play
		goto *adv_casinoCard
	}
	goto *com_txtAdvQuit

*com_txtadv_loop
	redraw 0
	screenupdate = -1
	gosub *screen_draw
	if ( atxid >= 2 ) {
		txtadvmsgfix = 20 + 116
	}
	repeat noteinfo(0)
		noteget s, cnt
		if ( strmid(s, 0, 1) == "@" ) {
			s(1) = strmid(s, 1, 2)
			s = strmid(s, 3, strlen(s) - 3)
			font lang(cfg_font1, cfg_font2), 16 - en * 2, 0
			color 250, 240, 230
			if ( s(1) == "BL" ) {
				color 130, 130, 250
			}
			if ( s(1) == "GR" ) {
				color 130, 250, 130
			}
			if ( s(1) == "QM" ) {
				color 0, 100, 0
			}
		}
		else {
			font lang(cfg_font1, cfg_font2), 16 - en * 2, 0
			color 250, 240, 230
		}
		pos 170, cnt * 20 + 120 + txtadvmsgfix
		mes s
	loop
	page_check
	gsel 2
	gmode 0
	pos 0, 0
	gcopy 0, 0, 0, windoww, windowh
	gsel 0
	gmode 2
	keyrange = 0
	repeat pagesize
		p = pagesize * page + cnt
		if ( p >= listmax ) {
			break
		}
		key_list(cnt) = key_select(cnt)
		keyrange++
	loop
	gosub *com_adv_anime1
	*com_txtadv_loop_WHILE1
	redraw 0
	x = 170, 400
	y = noteinfo(0) * 20 + 120 + txtadvmsgfix + 16, 20 * listmax
	gmode 0
	pos x, y
	gcopy 2, x, y, x(1), y(1)
	gmode 2
	font lang(cfg_font1, cfg_font2), 14 - en * 2, 0
	cs_listbk
	repeat pagesize
		p = pagesize * page + cnt
		if ( p >= listmax ) {
			break
		}
		i = list(0, p)
		display_key 170, noteinfo(0) * 20 + 120 + txtadvmsgfix + 16 + cnt * 20, cnt
		s = listn(0, p)
		cs_list s, 170 + 30, noteinfo(0) * 20 + 120 + txtadvmsgfix + 16 + cnt * 20, 19, 0, 2
	loop
	if ( keyrange != 0 ) {
		cs_bk = cs
	}
	redraw 1
	await cfg_wait1
	key_check
	cursor_check
	rtval = -1
	repeat keyrange
		if ( key == key_select(cnt) ) {
			rtval = list(0, pagesize * page + cnt)
			snd SOUNDLIST_CLICK1
			break
		}
	loop
	if ( chatesc != (-1) ) {
		if ( key == key_cancel ) {
			snd SOUNDLIST_CLICK1
			rtval = chatesc
		}
	}
	if ( rtval != (-1) ) {
		gosub *com_adv_anime2
		atxpic = 0
		return
	}
	page_change *com_txtadv_loop
	goto *com_txtadv_loop_WHILE1

*com_adv_anime1
	gosub *com_adv_animeSub
	if ( mattile != (-1) ) {
		mattile = rnd(mattile(2)) + mattile(1)
		if ( mattile == 496 ) { mattile = 424 } // Ano moved rock, like why.
	}
	repeat 12
		x = 170, 300
		y = noteinfo(0) * 20 + 120 + txtadvmsgfix + 16, 20 * listmax
		redraw 0
		gmode 0
		pos x - 50, y - 50
		gcopy 2, x - 50, y - 50, 100 + x(1), y(1) + 100
		if ( cnt == 11 ) {
			break
		}
		color 0, 0, 0
		gmode 4, x(1), y(1), cnt * 25
		pos x + x(1) / 2 - 10 + cnt, y + y(1) / 2
		grotate 2, 0, 0, 0, x(1), y(1)
		if ( atxpic != 0 ) {
			x = 170 + 175, atxpic(2) + 120
			y = 170, atxpic(3)
			gmode 0
			pos x - atxpic(2) / 2, y - atxpic(3) / 2
			gcopy 2, x - atxpic(2) / 2, y - atxpic(3) / 2, x(1), y(1)
			pos x, y
			gmode 2, inf_tiles, inf_tiles
			if ( cnt == 10 ) {
				p_double = 0
			}
			else {
				p_double = 0.6222 * cnt
			}
			p(1) = 1
			if ( atxpic == 2 ) {
				p(1) = 5
			}
			grotate p(1), atxpic(1) \ 33 * 48, atxpic(1) / 33 * 48, p_double, cnt * (atxpic(2) / 10), cnt * (atxpic(3) / 10)
		}
		if ( mattile != (-1) ) {
			cnt2 = cnt
			repeat 2
				x = cnt * 250 + 170, 96
				y = 120, 96
				gmode 0
				pos x, y
				gcopy 2, x, y, x(1), y(1)
				pos x + x(1) / 2, y + y(1) / 2
				gmode 2, inf_tiles, inf_tiles
				grotate 1, mattile \ 33 * 48, mattile / 33 * 48, 0, cnt2 * (96 / 10), cnt2 * (96 / 10)
			loop
		}
		await 20
		redraw 1
	loop
	gmode 2
	atxpic = 0
	return

*com_adv_anime2
	gosub *com_adv_animeSub
	repeat 11
		x = 170
		y = noteinfo(0) * 20 + 120 + txtadvmsgfix + 16, 20 * listmax
		redraw 0
		gmode 0
		pos x - 50, y - 50
		gcopy 2, x - 50, y - 50, 100 + x(1), y(1) + 100
		color 0, 0, 0
		gmode 4, x(1), y(1), 250 - cnt * 25
		pos x + x(1) / 2 - 2 * cnt, y + y(1) / 2
		grotate 2, 0, 0, 0, x(1), y(1)
		await 15
		redraw 1
	loop
	return

*com_adv_animeSub
	x(1) = 300
	cs = -1
	redraw 0
	color 0, 0, 0
	boxf 170, noteinfo(0) * 20 + 120 + txtadvmsgfix + 16, 170 + x(1), noteinfo(0) * 20 + 120 + txtadvmsgfix + 16 + 20 * listmax
	font lang(cfg_font1, cfg_font2), 14 - en * 2, 0
	cs_listbk
	repeat pagesize
		p = pagesize * page + cnt
		if ( p >= listmax ) {
			break
		}
		i = list(0, p)
		display_key 170, noteinfo(0) * 20 + 120 + txtadvmsgfix + 16 + cnt * 20, cnt
		s = listn(0, p)
		gmode 2
		color 0, 0, 0
		cs_list s, 170 + 30, noteinfo(0) * 20 + 120 + txtadvmsgfix + 16 + cnt * 20, 19, 0, 2
	loop
	if ( keyrange != 0 ) {
		cs_bk = cs
	}
	gsel 2
	gmode 0
	pos 0, 0
	gcopy 0, 170, noteinfo(0) * 20 + 120 + txtadvmsgfix + 16, x(1), 20 * listmax
	gsel 0
	gmode 2
	cs = 0
	return

*com_txtAdvQuit
	mtilefilecur = -1
	gosub *map_prepareTile
	f = 0
	inv_getheader -1
	repeat invrange, invhead
		if ( inv(INV_ITEM_NUM, cnt) != 0 ) {
			f = 1
		}
	loop
	if ( f == 1 ) {
		if ( cdata(CDATA_HP, CHARA_PLAYER) >= 0 ) {
			txt lang("幾つかの戦利品がある。", "There're some items you can acquire.")
			screenupdate = -1
			gosub *screen_draw
			invsubroutine = 1
			invctrl = 22, 0
			gosub *com_inventory
		}
	}
	mode = MODELIST_MAIN
	atxid = 0
	exittempinv
	await 100
	snd SOUNDLIST_POP3
	gosub *music_play
	casino = 0
	return

*atx_RE
	casino = 1
	atxap = 10
	atxspot = ATX_GENERAL1
	atxinfon(0) = lang("ランダムサイト", "Random Site")
	atxinit
	atxthrough = 1
	if ( atxid(1) == ATX_RANDOM ) {
		atxid(1) = ATX_FIELD
		atxlv = gdata(GDATA_LEVEL)
		if ( mdata(MDATA_TYPE) == MAP_TYPE_DUNGEON ) {
			atxid(1) = ATX_DUNGEON
		}
		if ( mdata(MDATA_TYPE) == MAP_TYPE_TOWER ) {
			atxid(1) = ATX_BUILDING
		}
		if ( mdata(MDATA_TYPE) == MAP_TYPE_FOREST ) {
			atxid(1) = ATX_FOREST
		}
		if ( mdata(MDATA_TYPE) == MAP_TYPE_FORT ) {
			atxid(1) = ATX_BUILDING
		}
		if ( mdata(MDATA_TYPE) == MAP_TYPE_WORLD ) {
			atxlv = cdata(CDATA_LEVEL, CHARA_PLAYER)
			if ( 4 <= gdata(GDATA_WORLD_TILE) & gdata(GDATA_WORLD_TILE) < 9 ) {
				atxid(1) = ATX_FOREST
			}
			if ( 264 <= gdata(GDATA_WORLD_TILE) & gdata(GDATA_WORLD_TILE) < 363 ) {
				atxid(1) = ATX_FIELD
			}
			if ( 9 <= gdata(GDATA_WORLD_TILE) & gdata(GDATA_WORLD_TILE) < 13 ) {
				atxid(1) = ATX_FOREST
			}
			if ( 13 <= gdata(GDATA_WORLD_TILE) & gdata(GDATA_WORLD_TILE) < 17 ) {
				atxid(1) = ATX_FIELD
			}
		}
	}
	atxbg = "bg21"
	mattile = 0, 495, 3
	if ( atxid(1) == ATX_REMAIN ) {
		atxbg = "bg21"
		mattile = 0, 495, 3
		atxspot = ATX_REMAIN1
		noteadd lang("この辺りは残骸やら遺品やらでごちゃごちゃだ。", "You see a jumble of debris and remains left by the dead.")
	}
	if ( atxid(1) == ATX_BUSH ) {
		atxbg = "bg20"
		mattile = 0, 495, 3
		atxspot = ATX_BUSH1
		noteadd lang("この辺りには見たこともない植物がたくさんある。", "You see many plants you have never seen before.")
	}
	if ( atxid(1) == ATX_MINE ) {
		atxbg = "bg19"
		mattile = 0, 495, 3
		atxspot = ATX_MINE1
		noteadd lang("天然の鉱石の宝庫だ。", "You see an abundance of natural ores.")
	}
	if ( atxid(1) == ATX_WATER ) {
		atxbg = "bg18"
		mattile = 0, 495, 3
		atxspot = ATX_WATER1
		noteadd lang("綺麗な泉がある。", "You see a beautiful spring.")
	}
	if ( atxid(1) == ATX_DUNGEON ) {
		atxbg = "bg13"
		mattile = 0, 495, 3
		atxspot = ATX_DUNGEON1
	}
	if ( atxid(1) == ATX_BUILDING ) {
		atxbg = "bg17"
		mattile = 0, 495, 3
		atxspot = ATX_BUILDING1
	}
	if ( atxid(1) == ATX_FOREST ) {
		atxbg = "bg15"
		mattile = 0, 495, 3
		atxspot = ATX_FOREST1
	}
	if ( atxid(1) == ATX_FIELD ) {
		atxbg = "bg16"
		mattile = 0, 495, 3
		atxspot = ATX_FIELD1
	}
*atx_RE_main
	if ( atxap <= 0 | cdata(CDATA_HP, CHARA_PLAYER) < 0 ) {
		goto *atx_RE_end
	}
	atxinit
	noteadd lang("何をしよう？", "What will you do?")
	chatList 1, lang("探索", "Search")
	chatList 3, lang("去る", "Leave")
	atxinfon(1) = lang("行動回数残り " + atxap + "回\n", "Remaining Actions: " + atxap + "\n")
	atxinfon(2) = ""
	atxinfon(3) = lang(" 危険Lv" + atxlv + "　", " Danger-Lv: " + atxlv + "")
	chatesc = 3
	txtadvmsgfix = 0
	txtadvscreenupdate = 1
	gosub *com_txtadv_loop
	if ( rtval == 1 ) {
		goto *atx_RE_search
	}
	if ( rtval == 2 ) {
		goto *atx_RE_search
	}
	if ( rtval == 3 ) {
		goto *atx_RE_end
	}
	goto *atx_RE_main

*atx_RE_search
	atxap--
	atxinit
	if ( rnd(2) == 0 ) {
		atxtype = atxid(1)
		if ( rnd(5) == 0 | atxtype == ATX_RANDOM ) { atxtype = 1 + rnd(8) }
		atxrefval1 = -1
		atxskillcheck1 = SKILL_ATTR_STR
		atxskillcheck2 = SKILL_ATTR_PER
		if ( atxtype == ATX_BUSH ) {
			atxpic = 1, 171, 96, 96
			atxskillcheck1 = SKILL_NORMAL_DETECTION
			atxskillcheck2 = SKILL_NORMAL_GARDENING
			noteadd lang("茂みを見つけた。", "You found a bush.")
			chatList 1, lang("調べる", "Investigate ") + "(" + skillname(atxskillcheck1) + ": " + sdata(atxskillcheck1, CHARA_PLAYER) + ")"
			chatList 2, lang("採取する", "Harvest ") + "(" + skillname(atxskillcheck2) + ": " + sdata(atxskillcheck2, CHARA_PLAYER) + ")"
			atxrefval1 = 3
		}
		if ( atxtype == ATX_MINE ) {
			atxpic = 1, 219, 96, 96
			atxskillcheck1 = SKILL_NORMAL_JEWELER
			atxskillcheck2 = SKILL_NORMAL_MINING
			noteadd lang("鉱石の岩がある。", "You see a vein of ore in the rocks.")
			chatList 1, lang("調べる", "Investigate ") + "(" + skillname(atxskillcheck1) + ": " + sdata(atxskillcheck1, CHARA_PLAYER) + ")"
			chatList 2, lang("掘る", "Dig ") + "(" + skillname(atxskillcheck2) + ": " + sdata(atxskillcheck2, CHARA_PLAYER) + ")"
			atxrefval1 = 1
		}
		if ( atxtype == ATX_WATER ) {
			atxpic = 1, 439, 96, 96
			atxskillcheck1 = SKILL_ATTR_LUC
			atxskillcheck2 = SKILL_NORMAL_ANATOMY
			noteadd lang("泉がある。", "You found a spring of water.")
			chatList 1, lang("飲む", "Drink") + "(" + skillname(atxskillcheck1) + ": " + sdata(atxskillcheck1, CHARA_PLAYER) + ")"
			chatList 2, lang("釣る", "Fish ") + "(" + skillname(atxskillcheck2) + ": " + sdata(atxskillcheck2, CHARA_PLAYER) + ")"
			atxrefval1 = 2
		}
		if ( atxtype == ATX_REMAIN ) {
			atxpic = 1, 199, 96, 96
			atxskillcheck1 = SKILL_NORMAL_GENE_ENGINEER
			atxskillcheck2 = SKILL_NORMAL_ANATOMY
			noteadd lang("残骸を見つけた。", "You found some remains.")
			chatList 1, lang("あさる", "Scavenge ") + "(" + skillname(atxskillcheck1) + ": " + sdata(atxskillcheck1, CHARA_PLAYER) + ")"
			chatList 2, lang("解剖する", "Dissect ") + "(" + skillname(atxskillcheck2) + ": " + sdata(atxskillcheck2, CHARA_PLAYER) + ")"
			atxrefval1 = 4
		}
		if ( atxtype == ATX_FOREST ) {
			atxpic = 1, 239, 96, 96
			atxskillcheck1 = SKILL_NORMAL_CARPENTRY
			atxskillcheck2 = SKILL_NORMAL_WEIGHT_LIFTING
			noteadd lang("残骸を見つけた。", "You found heavy wood blocking the path.")
			chatList 1, lang("あさる", "Chop ") + "(" + skillname(atxskillcheck1) + ": " + sdata(atxskillcheck1, CHARA_PLAYER) + ")"
			chatList 2, lang("解剖する", "Lift ") + "(" + skillname(atxskillcheck2) + ": " + sdata(atxskillcheck2, CHARA_PLAYER) + ")"
			atxrefval1 = 5
		}
		if ( atxtype == ATX_DUNGEON ) {
			atxpic = 2, 378, 96, 96
			atxskillcheck1 = SKILL_NORMAL_TACTICS
			atxskillcheck2 = SKILL_NORMAL_STEALTH
			noteadd lang("カミカゼを見つけたね。", "You found some kamikaze-yeeks ahead.")
			chatList 1, lang("接近", "Approach ") + "(" + skillname(atxskillcheck1) + ": " + sdata(atxskillcheck1, CHARA_PLAYER) + ")"
			chatList 2, lang("解剖する", "Sneak attack ") + "(" + skillname(atxskillcheck2) + ": " + sdata(atxskillcheck2, CHARA_PLAYER) + ")"
			atxrefval1 = 5
		}
		if ( atxtype == ATX_BUILDING ) {
			atxpic = 1, 470, 96, 96
			atxskillcheck1 = SKILL_NORMAL_LITERACY
			atxskillcheck2 = SKILL_NORMAL_MEMORIZATION
			noteadd lang("残骸を見つけた。", "You found some familiar magical inscriptions.")
			chatList 1, lang("あさる", "Just read it ") + "(" + skillname(atxskillcheck1) + ": " + sdata(atxskillcheck1, CHARA_PLAYER) + ")"
			chatList 2, lang("解剖する", "Try to recall ") + "(" + skillname(atxskillcheck2) + ": " + sdata(atxskillcheck2, CHARA_PLAYER) + ")"
			atxrefval1 = 5
		}
		if ( atxrefval1 == (-1) ) {
			noteadd lang("壁に何やら怪しいひび割れがある…", "There is some kind of suspicious crack on the wall...")
			chatList 1, lang("叩く", "Whack") + "(" + skillname(atxskillcheck1) + ": " + sdata(atxskillcheck1, CHARA_PLAYER) + ")"
			chatList 2, lang("調べる", "Investigate") + "(" + skillname(atxskillcheck2) + ": " + sdata(atxskillcheck2, CHARA_PLAYER) + ")"
			atxrefval1 = 1 + rnd(5)
		}
		chatList 3, lang("去る", "Leave")
		atxinfon(1) = lang("行動回数残り " + atxap + "回\n", "Remaining Actions: " + atxap + "\n")
		chatesc = 1
		txtadvmsgfix = 0
		txtadvscreenupdate = 1
		gosub *com_txtadv_loop
		atxinit
		if ( rtval == 2 ) {
			atxskillcheck1 = atxskillcheck2
			rtval = 1
		}
		if ( rtval == 1 ) {
			if ( rnd(limit(atxlv, 1, 2000)) > rnd(limit(sdata(atxskillcheck1, CHARA_PLAYER), 1, 2000) * 2 + 5) & rnd(10) != 0 ) {
				skillexp atxskillcheck1, CHARA_PLAYER, 100
				noteadd lang("うっ！その過程で自分を傷つけた。", "Ugh! You hurt yourself during the process.")
				snd SOUNDLIST_ATK1
				dmghp CHARA_PLAYER, cdata(CDATA_MAX_HP, CHARA_PLAYER) * 5 / 100, DAMAGE_FROM_ATX
				atxthrough = 1
				goto *atx_RE_main
			}
			skillexp atxskillcheck1, CHARA_PLAYER, 50
			repeat 6
				if ( cnt >= 3 & rnd(limit(atxlv, 1, 2000)) > rnd(limit(sdata(atxskillcheck1, CHARA_PLAYER) * cnt / 2, 1, 2000)) ) {
					continue
				}
				p = random_material(atxlv, 0)
				matgetmain p, 1, 5
				if ( rnd(limit(atxlv, 1, 2000)) > rnd(limit(sdata(atxskillcheck1, CHARA_PLAYER) / 2, 1, 2000)) ) {
					matgetmain p, 1, 5
				}
				snd SOUNDLIST_GET3
				atxpic = 1, matref(MATREF_PIC, p), 96, 96
				noteadd "@BL" + locvar_matgetmain_s
			loop
			atxthrough = 1
			goto *atx_RE_main
		}
		if ( rtval == 3 ) {
			atxthrough = 1
			goto *atx_RE_main
		}
	}
	if ( rnd(8) == 0 ) {
		if ( rnd(4) == 0 ) {
			noteadd lang("あれ…？", "Wait.. what?")
			noteadd lang("道に迷った！ (行動回数-2)", "You got lost! (Actions -2)")
			atxap -= 2
			atxthrough = 1
			goto *atx_RE_main
		}
		if ( rnd(2) == 0 ) {
			atxpic = 2, 205, 96, 96
			noteadd lang("幽霊に脅かされた！ (行動回数-1)", "You got spooked by a ghost! (Actions -1)")
			snd SOUNDLIST_TRAP1
			atxap -= 1
			atxthrough = 1
			goto *atx_RE_main
		}
		if ( rnd(2) == 0 ) {
			atxpic = 1, 424, 96, 96
			noteadd lang("小石につまづいてころんでしまった！ (行動回数-1)", "You tripped and fell on a pebble! (Actions -1)")
			snd SOUNDLIST_TRAP1
			atxap -= 1
			atxthrough = 1
			goto *atx_RE_main
		}
	}
	if ( rnd(8) == 0 ) {
		if ( rnd(4) == 0 ) {
			atxpic = 1, 200, 96, 96
			noteadd lang("ふかふかの藁を見つけた。", "I found a pile of fluffy straw.")
			noteadd lang("ふかふかして気持ちいい。（行動回数+3)", "Fluffy, mmmm, fluffy. (Actions +3)")
			atxap += 2
			atxthrough = 1
			goto *atx_RE_main
		}
		if ( rnd(2) == 0 ) {
			atxpic = 1, 294, 96, 96
			noteadd lang("切り株がある。", "You found a stump.")
			noteadd lang("腰を下ろして疲れを癒した。（行動回数+2)", "You sat back and relaxed from the fatigue. (Actions +2)")
			atxap += 2
			atxthrough = 1
			goto *atx_RE_main
		}
		if ( rnd(2) == 0 ) {
			atxpic = 1, 127, 96, 96
			noteadd lang("綺麗な花を見つけた。", "You found a beautiful flower.")
			noteadd lang("心がなごんだ…（行動回数+2)", "Your heart is comforted. (Actions +2)")
			atxap += 2
			atxthrough = 1
			goto *atx_RE_main
		}
	}
	if ( rnd(7) == 0 & cdata(CDATA_MAX_HP, CHARA_PLAYER) > cdata(CDATA_HP, CHARA_PLAYER) ) {
		if ( rnd(3) ) {
			atxpic = 1, 185, 96, 96
			noteadd lang("ああ、癒しのハーブだ。", "Aha! Some healing herbs.")
			snd SOUNDLIST_ATK1
			healhp CHARA_PLAYER, cdata(CDATA_MAX_HP, CHARA_PLAYER) * 5 / 100, 0
		}
		else {
			atxpic = 1, 171, 96, 96
			noteadd lang("あっ！包帯用のつるだ。", "Oh! Vines for bandaging.")
			snd SOUNDLIST_ATK1
			healhp CHARA_PLAYER, cdata(CDATA_MAX_HP, CHARA_PLAYER) * 10 / 100, 0
		}
		atxthrough = 1
		goto *atx_RE_main
	}
	if ( rnd(3) == 0 ) {
		if ( rnd(3) ) {
			atxpic = 2, 210, 96, 96
			noteadd lang("痛っ！蚊に刺された。", "Ouch! Mosquito bites.")
			snd SOUNDLIST_ATK1
			dmghp CHARA_PLAYER, cdata(CDATA_MAX_HP, CHARA_PLAYER) * 5 / 100, DAMAGE_FROM_ATX
		}
		else {
			atxpic = 2, 216, 96, 96
			noteadd lang("うっ！ヘビに噛まれた。", "Ugh! Snake bite.")
			snd SOUNDLIST_ATK1
			dmghp CHARA_PLAYER, cdata(CDATA_MAX_HP, CHARA_PLAYER) * 10 / 100, DAMAGE_FROM_ATX
		}
		atxthrough = 1
		goto *atx_RE_main
	}
	if ( rnd(2) == 0 ) {
		atxpic = 1, 220, 96, 96
		noteadd lang("宝箱がある。", "You found a chest!")
		chatList 1, lang("錠を解体する", "Dismantle the lock. ") + "(" + skillname(SKILL_NORMAL_LOCK_PICKING) + ": " + sdata(SKILL_NORMAL_LOCK_PICKING, CHARA_PLAYER) + ")"
		chatList 2, lang("叩き割る(筋力: ", "Smash it. ") + "(" + skillname(SKILL_ATTR_STR) + ": " + sdata(SKILL_ATTR_STR, CHARA_PLAYER) + ")"
		chatList 3, lang("去る", "Leave")
		atxinfon(1) = lang("行動回数残り " + atxap + "回\n", "Remaining actions: " + atxap + "\n")
		keihin = rnd(30) + 2
		nannido = keihin * keihin
		atxinfon(3) = lang(" 景品Lv" + keihin + "　", " Chest-Rank: " + keihin + "")
		chatesc = 1
		txtadvmsgfix = 0
		txtadvscreenupdate = 1
		gosub *com_txtadv_loop
		atxinit
		if ( rtval == 1 ) {
			item_find ITEM_ID_LOCKPICK, 3
			if ( stat == (-1) ) {
				noteadd lang("ロックピックを所持していない。", "You don't have lockpicks.")
				atxthrough = 1
				goto *atx_RE_main
			}
			if ( rnd(nannido) > rnd(limit(sdata(SKILL_NORMAL_LOCK_PICKING, CHARA_PLAYER), 1, 2000)) ) {
				noteadd lang("開錠に失敗した。", "You fail to unlock it.")
				atxthrough = 1
				goto *atx_RE_main
			}
			skillexp SKILL_NORMAL_LOCK_PICKING, cc, 100 + limit(nannido * 2, 1, 400)
			repeat 1
				i = 2
				if ( keihin >= 7 ) { i = 3 }
				if ( keihin >= 14 ) { i = 4 }
				if ( keihin >= 21 ) { i = 4 }
				if ( keihin >= 28 ) { i = 5 }
				flt calcobjlv(rnd(keihin + 1) + keihin), i
				flttypemajor = fsetwear(rnd(length(fsetwear)))
				itemcreate -1, ITEM_ID_DUMMY, -1, -1, 0
				if ( inv(INV_ITEM_NUM, ci) == 0 ) {
					continue cnt
				}
			loop
			snd SOUNDLIST_GET3
			noteadd lang("@GRアイテム：" + itemname(ci, inv(INV_ITEM_NUM, ci)) + "を戦利品に加えた！", "@GR" + itemname(ci, inv(INV_ITEM_NUM, ci)) + " has been added to your loot list!")
			noteadd lang("開錠に成功した。", "You successfully unlock it.")
			atxthrough = 1
			goto *atx_RE_main
		}
		if ( rtval == 2 ) {
			if ( rnd(nannido) > rnd(limit(sdata(SKILL_ATTR_STR, CHARA_PLAYER), 1, 2000)) ) {
				noteadd lang("筋肉を痛めた。", "You hurt your muscles.")
				atxthrough = 1
				goto *atx_RE_main
			}
			skillexp SKILL_ATTR_STR, CHARA_PLAYER, nannido
			if ( rnd(2) == 0 ) {
				noteadd lang("体当たりして破壊した。", "You bash up chest. The content is destroyed.")
				atxthrough = 1
				goto *atx_RE_main
			}
			repeat 1
				i = 2
				if ( keihin >= 7 ) { i = 3 }
				if ( keihin >= 14 ) { i = 4 }
				if ( keihin >= 21 ) { i = 4 }
				if ( keihin >= 28 ) { i = 5 }
				flt calcobjlv(rnd(keihin + 1) + keihin), i
				flttypemajor = fsetwear(rnd(length(fsetwear)))
				itemcreate -1, ITEM_ID_DUMMY, -1, -1, 0
				if ( inv(INV_ITEM_NUM, ci) == 0 ) {
					continue cnt
				}
			loop
			snd SOUNDLIST_GET3
			noteadd lang("@GRアイテム：" + itemname(ci, inv(INV_ITEM_NUM, ci)) + "を戦利品に加えた！", "@GR" + itemname(ci, inv(INV_ITEM_NUM, ci)) + " has been added to your loot list!")
			noteadd lang("開錠に成功した。", "You successfully unlock it.")
			atxthrough = 1
			goto *atx_RE_main
		}
		goto *atx_RE_main
	}
	if ( rnd(2) == 0 ) {
		p = random_material(atxlv)
		atxpic = 1, matref(MATREF_PIC, p), 96, 96
		snd SOUNDLIST_GET3
		mat(p) += 1
		noteadd lang("@BL" + matname(p) + "を" + 1 + "個手に入れた！(所持数:" + mat(p) + "個)", "@BLYou get " + 1 + " " + matname(p) + "(s)! (Total:" + mat(p) + ")")
		atxthrough = 1
		goto *atx_RE_main
	}
	if ( rnd(3) == 0 ) {
		noteadd lang("何も見つからなかった…", "You found nothing...")
		atxthrough = 1
		goto *atx_RE_main
	}
	noteadd lang("何も見つからなかった…", "You found nothing...")
	atxthrough = 1
	goto *atx_RE_main

*atx_RE_end
	atxinit
	if ( cdata(CDATA_HP, CHARA_PLAYER) >= 0 ) {
		noteadd lang("探索を終えた。", "Finished searching.")
		chatList 0, lang("戻る", "Return")
	}
	else {
		noteadd lang("ぐふ…", "Aieeeeeeeee...")
		chatList 0, lang("(断末魔の叫び)", "(Death Cry)")
	}
	atxinfon(1) = lang("行動回数残り " + atxap + "回\n", "Remaining actions: " + atxap + "\n")
	chatesc = 0
	txtadvmsgfix = 0
	txtadvscreenupdate = 1
	gosub *com_txtadv_loop
	goto *com_txtAdvQuit

*adv_casinoCard
	casino = 1
	atxbg = "bg14"
	mattile = -1
	atxinfon(0) = lang("カジノ《フォーチュンクッキー》", "Casino <<Fortune Cookie>>")
	atxinit
	noteadd lang("カジノ《フォーチュンクッキー》へようこそ。", "Welcome to the casino, Fortune cookie!")
	noteadd lang("チップマテリアルと引き換えにゲームをすることができます。", "You can bet the casino chips you have and play some games.")
	noteadd lang("ごゆっくりお楽しみ下さい。", "Enjoy your stay.")
	if ( gdata(GDATA_FLAG_CASINO) == 0 ) {
		noteadd ""
		noteadd lang("お客様は初めてのご利用のようですね。", "Looks like you play for the first time, sir.")
		noteadd lang("当店からチップマテリアルを10枚進呈します。", "We're offering you 10 free casino chips to try our games.")
		gdata(GDATA_FLAG_CASINO) = 1
		snd SOUNDLIST_GET3
		mat(1) += 10
		noteadd lang("@BL" + matname(1) + "を" + 10 + "個手に入れた！(所持数:" + mat(1) + "個)", "@BLYou get " + 10 + " " + matname(1) + "(s)! (Total:" + mat(1) + ")")
	}
	atxinfon(1) = lang("カジノチップ残り " + mat(1) + "枚\n", "Casino chips left: " + mat(1) + "\n")
	atxinfon(2) = ""
	chatList 0, lang("店を出る", "Later.")
	chatList 1, lang("ブラックジャック", "I want to play Blackjack.")
	/********** JAMES CUSTOM - SLOTS - BEGINNING **********/
	if ( atxci == ITEM_ID_SLOT_MACHINE | atxci == ITEM_ID_PACHISURO_MACHINE ) {
		listmax = 0
		chatList 0, lang("店を出る", "Later.")
		chatList 1, lang("ブラックジャック", "I want to play Slots.")
	}
	/********** JAMES CUSTOM - SLOTS - ENDING **********/
	chatesc = 0
	txtadvmsgfix = 0
	txtadvscreenupdate = 1
	gosub *com_txtadv_loop
	if ( rtval == 0 ) {
		goto *com_txtAdvQuit
	}
	if ( rtval == 1 ) {
		/********** JAMES CUSTOM - SLOTS - BEGINNING **********/
		if ( atxci == ITEM_ID_SLOT_MACHINE | atxci == ITEM_ID_PACHISURO_MACHINE ) {
			goto *adv_casinoSlots
		}
		/********** JAMES CUSTOM - SLOTS - ENDING **********/
		goto *adv_casinoBlackJack
	}
	goto *adv_casinoCard

*adv_casinoBlackJack
	ikasama = 0
	keihin = 0
	ikasamalv = 0
	atxinit
	noteadd lang("ブラックジャックは、カードの合計を21に近づけるゲームです。", "In Blackjack, the hand with the highest total wins as long as it")
	noteadd lang("J,Q,Kは10に、Aは1または11に数えられます。21を越えると負けです。", "doesn't exceed 21. J,Q,K are counted as 10 and A is counted as 1 or 11.")
	noteadd lang("では、賭けるチップを宣言してください。", "More bets means better rewards.")
	noteadd lang("チップが多いほど、景品の質があがります。", "How many tips would you like to bet?")
	atxinfon(1) = lang("カジノチップ残り " + mat(1) + "枚\n", "Casino chips left: " + mat(1) + "\n")
	atxinfon(2) = ""
	atxinfon(3) = ""
	if ( mat(1) <= 0 ) {
		noteadd ""
		noteadd lang("お客様はチップをもっていません。", "Sorry sir, you don't seem to have casino chips.")
	}
	chatList 0, lang("やめる", "I quit.")
	if ( mat(1) >= 1 ) {
		chatList 1, lang("" + 1 + "枚賭ける", "Bet " + 1 + " chips.")
	}
	if ( mat(1) >= 5 ) {
		chatList 5, lang("" + 5 + "枚賭ける", "Bet " + 5 + " chips.")
	}
	if ( mat(1) >= 10 ) {
		chatList 10, lang("" + 10 + "枚賭ける", "Bet " + 10 + " chips.")
	}
	if ( mat(1) >= 20 ) {
		chatList 20, lang("" + 20 + "枚賭ける", "Bet " + 20 + " chips.")
	}
	chatesc = 0
	txtadvmsgfix = 0
	txtadvscreenupdate = 1
	gosub *com_txtadv_loop
	if ( rtval == 0 ) {
		goto *adv_casinoCard
	}
	stake = rtval
	winrow = 0
	cardround = 0
	autosave = 1 * (gdata(GDATA_AREA) != AREA_SHOW_HOUSE)
	repeat
		screenupdate = -1
		gosub *screen_draw
		atxinit
		if ( cnt == 0 ) {
			mat(1) -= stake
			txt lang(matname(1) + "を" + stake + "個失った(残り:" + mat(1) + "個)", "You lose " + stake + " " + matname(1) + "(s). (Total:" + mat(1) + ")")
		}
		if ( cardround == 0 ) {
			nojoker = 1
			initcard 60, 160
			cardplayerinit 2, 5
			cardplayeradd 0, 220, 124
			cardplayeradd 1, 220, 240
		}
		font lang(cfg_font1, cfg_font2), 14 - en * 2, 1
		color 255, 255, 255
		pos 152, 154
		mes lang("　親", "Dealer")
		pos 152, 270
		mes lang("あなた", "   You")
		showcardpile
		showcardholder
		showcard
		if ( cardround == 0 ) {
			repeat 4
				servecard cnt \ 2
				if ( cnt != 2 ) {
					opencard2 stat
				}
			loop
			repeat 1
				if ( cpcardnum(0) >= 5 ) {
					break
				}
				if ( cpscore(0) < cpscore(1) | cpscore(0) - rnd(5) <= 11 ) {
					servecard 0
					opencard2 stat
					continue cnt
				}
			loop
		}
		noteadd lang("あなたの合計は" + cpscore(1) + "です。", "Your hand is " + cpscore(1) + ".")
		if ( cardround == (-1) ) {
			noteadd lang("親の合計は" + cpscore(0) + "です。", "The dealer's hand is " + cpscore(0) + ".")
			winner = -1
			if ( cpscore(0) <= 21 ) {
				if ( cpscore(1) > 21 | cpscore(0) > cpscore(1) ) {
					winner = 0
				}
			}
			if ( cpscore(1) <= 21 ) {
				if ( cpscore(0) > 21 | cpscore(1) > cpscore(0) ) {
					winner = 1
				}
			}
			if ( winner == (-1) ) {
				noteadd lang("勝負は引き分けです。", "The match is a draw.")
			}
			if ( winner == 0 ) {
				noteadd lang("あなたの負けです。", "You lose.")
			}
			if ( winner == 1 ) {
				noteadd lang("おめでとうございます。あなたの勝ちです。", "Congratulations, you win.")
			}
		}
		ikasama = rnd(40) + ikasamalv * ikasamalv * 5
		keihin = stake + winrow * 2
		atxinfon(1) = lang("カジノチップ残り " + mat(1) + "枚\n", "Casino chips left: " + mat(1) + "\n")
		atxinfon(2) = lang("賭けチップ " + stake + "枚 現在" + winrow + "連勝中", "Bets: " + stake + " Wins: " + winrow + "")
		atxinfon(3) = lang("イカサマ警戒Lv" + ikasama + " 景品Lv" + keihin + "　", "Alert-Lv: " + ikasama + " Rewards-Lv: " + keihin + "")
		if ( cardround == (-1) ) {
			if ( winner == 1 ) {
				winrow++
			}
			if ( winner != 0 ) {
				chatList 0, lang("次の勝負へ", "To the next round.")
			}
			if ( winner == 0 ) {
				chatList 0, lang("戻る", "Bah...!")
			}
			chatesc = -1
			txtadvmsgfix = 240
			txtadvscreenupdate = 0
			gosub *com_txtadv_loop
			if ( winner == 0 ) {
				break
			}
			cardround = 0
			continue
		}
		chatList 0, lang("これに決める", "Stay.")
		if ( pileremain() > 10 ) {
			if ( cpcardnum(1) < 5 ) {
				chatList 1, lang("もう一枚引く(運)", "Hit me. (Luck)")
			}
			chatList 2, lang("イカサマ(器用", "Cheat. (Dex:") + sdata(SKILL_ATTR_DEX, CHARA_PLAYER) + ")"
			if ( stake >= 2 ) {
				chatList 3, lang("降りる(賭けチップ1枚減少)", "I'm out. (costs 1 chip)")
			}
		}
		chatesc = -1
		txtadvmsgfix = 240
		txtadvscreenupdate = 0
		gosub *com_txtadv_loop
		if ( rtval == 0 ) {
			opencard2 cpblackcard(0)
			cardround = -1
			continue
		}
		if ( rtval == 1 ) {
			servecard 1
			p = stat
			repeat 3
				if ( cpscore(1) >= 22 ) {
					if ( pileremain() > 10 ) {
						if ( rnd(sdata(SKILL_ATTR_LUC, CHARA_PLAYER)) > 40 ) {
							txt lang("このカードは悪い予感がする…", "I have a bad feeling about this card...")
							trashcard p
							servecard 1
							p = stat
						}
					}
				}
			loop
			opencard2 p
		}
		if ( rtval == 2 ) {
			trashcard lastcard(1)
			servecard 1
			opencard2 stat
			p = ikasama
			ikasamalv++
			if ( rnd(sdata(SKILL_ATTR_DEX, CHARA_PLAYER)) < rnd(p + 1) ) {
				atxinit
				noteadd lang("イカサマだ！", "Cheater!")
				atxinfon(1) = lang("カジノチップ残り " + mat(1) + "枚\n", "Casino chips left: " + mat(1) + "\n")
				atxinfon(2) = lang("賭けチップ " + stake + "枚 現在" + winrow + "連勝中", "Bets: " + stake + " Wins: " + winrow + "")
				atxinfon(3) = lang("イカサマ警戒Lv" + ikasama + " 景品Lv" + keihin + "　", "Alert-Lv: " + ikasama + " Rewards-Lv: " + keihin + "")
				winrow = 0
				txt lang("イカサマが見つかってしまった…", "You are caught cheating...")
				modkarma CHARA_PLAYER, -5
				chatList 0, lang("濡れ衣だ！", "I didn't do it!")
				chatesc = -1
				txtadvmsgfix = 0
				txtadvscreenupdate = 1
				gosub *com_txtadv_loop
				break
			}
		}
		if ( rtval == 3 ) {
			stake--
			cardround = 0
			continue
		}
		cardround++
	loop
	if ( winrow > 0 ) {
		atxinit
		noteadd lang("おめでとうございます。景品Lvは" + keihin + "でした。", "Congratulations! Your Rewards-Lv was " + keihin + "!")
		repeat 1
			i = 2
			if ( keihin >= 7 ) {
				i = 3
			}
			if ( keihin >= 14 ) {
				i = 4
			}
			if ( keihin >= 21 ) {
				i = 4
			}
			if ( keihin >= 28 ) {
				i = 5
			}
			flt calcobjlv(rnd(keihin + 1) + keihin), i
			flttypemajor = fsetwear(rnd(length(fsetwear)))
			itemcreate -1, ITEM_ID_DUMMY, -1, -1, 0
			if ( inv(INV_ITEM_NUM, ci) == 0 ) {
				continue cnt
			}
		loop
		snd SOUNDLIST_GET3
		noteadd lang("@GRアイテム：" + itemname(ci, inv(INV_ITEM_NUM, ci)) + "を戦利品に加えた！", "@GR" + itemname(ci, inv(INV_ITEM_NUM, ci)) + " has been added to your loot list!")
		if ( gdata(GDATA_FLAG_RARE_DROP) == 100 ) {
			if ( keihin >= 5 ) {
				flt
				itemcreate -1, ITEM_ID_GIFT, -1, -1, 0
				snd SOUNDLIST_GET3
				noteadd lang("@GRアイテム：" + itemname(ci, inv(INV_ITEM_NUM, ci)) + "を戦利品に加えた！", "@GR" + itemname(ci, inv(INV_ITEM_NUM, ci)) + " has been added to your loot list!")
			}
			if ( keihin >= 9 ) {
				flt
				itemcreate -1, ITEM_ID_POTION_CURE_CORRUPTION, -1, -1, 0
				snd SOUNDLIST_GET3
				noteadd lang("@GRアイテム：" + itemname(ci, inv(INV_ITEM_NUM, ci)) + "を戦利品に加えた！", "@GR" + itemname(ci, inv(INV_ITEM_NUM, ci)) + " has been added to your loot list!")
			}
			if ( keihin >= 11 ) {
				flt
				itemcreate -1, ITEM_ID_UNICORN_HORN, -1, -1, 0
				snd SOUNDLIST_GET3
				noteadd lang("@GRアイテム：" + itemname(ci, inv(INV_ITEM_NUM, ci)) + "を戦利品に加えた！", "@GR" + itemname(ci, inv(INV_ITEM_NUM, ci)) + " has been added to your loot list!")
			}
			if ( keihin >= 13 ) {
				flt
				itemcreate -1, ITEM_ID_BLANK_BUSINESS_CARD, -1, -1, 0
				snd SOUNDLIST_GET3
				noteadd lang("@GRアイテム：" + itemname(ci, inv(INV_ITEM_NUM, ci)) + "を戦利品に加えた！", "@GR" + itemname(ci, inv(INV_ITEM_NUM, ci)) + " has been added to your loot list!")
			}
			if ( keihin >= 15 ) {
				flt
				itemcreate -1, ITEM_ID_SCROLL_FAITH, -1, -1, 0
				snd SOUNDLIST_GET3
				noteadd lang("@GRアイテム：" + itemname(ci, inv(INV_ITEM_NUM, ci)) + "を戦利品に加えた！", "@GR" + itemname(ci, inv(INV_ITEM_NUM, ci)) + " has been added to your loot list!")
			}
			if ( keihin >= 17 ) {
				flt
				itemcreate -1, ITEM_ID_POTION_WISDOM, -1, -1, 0
				snd SOUNDLIST_GET3
				noteadd lang("@GRアイテム：" + itemname(ci, inv(INV_ITEM_NUM, ci)) + "を戦利品に加えた！", "@GR" + itemname(ci, inv(INV_ITEM_NUM, ci)) + " has been added to your loot list!")
			}
			if ( keihin >= 19 ) {
				flt
				itemcreate -1, ITEM_ID_GOLDEN_MEAD, -1, -1, 0
				snd SOUNDLIST_GET3
				noteadd lang("@GRアイテム：" + itemname(ci, inv(INV_ITEM_NUM, ci)) + "を戦利品に加えた！", "@GR" + itemname(ci, inv(INV_ITEM_NUM, ci)) + " has been added to your loot list!")
			}
			if ( keihin >= 23 ) {
				flt
				itemcreate -1, ITEM_ID_POTION_POTENTIAL, -1, -1, 0
				snd SOUNDLIST_GET3
				noteadd lang("@GRアイテム：" + itemname(ci, inv(INV_ITEM_NUM, ci)) + "を戦利品に加えた！", "@GR" + itemname(ci, inv(INV_ITEM_NUM, ci)) + " has been added to your loot list!")
			}
			if ( keihin >= 25 ) {
				flt
				itemcreate -1, ITEM_ID_SURVIVABILITY_EXTENDER_X, -1, -1, 0
				snd SOUNDLIST_GET3
				noteadd lang("@GRアイテム：" + itemname(ci, inv(INV_ITEM_NUM, ci)) + "を戦利品に加えた！", "@GR" + itemname(ci, inv(INV_ITEM_NUM, ci)) + " has been added to your loot list!")
				flt
				itemcreate -1, ITEM_ID_SURVIVABILITY_EXTENDER_Y, -1, -1, 0
				snd SOUNDLIST_GET3
				noteadd lang("@GRアイテム：" + itemname(ci, inv(INV_ITEM_NUM, ci)) + "を戦利品に加えた！", "@GR" + itemname(ci, inv(INV_ITEM_NUM, ci)) + " has been added to your loot list!")
			}
			if ( keihin >= 27 ) {
				if ( gdata(GDATA_FLAG_CASINO) == 1 ) {
					gdata(GDATA_FLAG_CASINO) = 2
					flt
					itemcreate -1, ITEM_ID_MASTER_OF_DIAMOND, -1, -1, 0
					snd SOUNDLIST_GET3
					noteadd lang("@GRアイテム：" + itemname(ci, inv(INV_ITEM_NUM, ci)) + "を戦利品に加えた！", "@GR" + itemname(ci, inv(INV_ITEM_NUM, ci)) + " has been added to your loot list!")
					flt
					itemcreate -1, ITEM_ID_MASTER_OF_HEART, -1, -1, 0
					snd SOUNDLIST_GET3
					noteadd lang("@GRアイテム：" + itemname(ci, inv(INV_ITEM_NUM, ci)) + "を戦利品に加えた！", "@GR" + itemname(ci, inv(INV_ITEM_NUM, ci)) + " has been added to your loot list!")
					flt
					itemcreate -1, ITEM_ID_MASTER_OF_CLUB, -1, -1, 0
					snd SOUNDLIST_GET3
					noteadd lang("@GRアイテム：" + itemname(ci, inv(INV_ITEM_NUM, ci)) + "を戦利品に加えた！", "@GR" + itemname(ci, inv(INV_ITEM_NUM, ci)) + " has been added to your loot list!")
					flt
					itemcreate -1, ITEM_ID_MASTER_OF_SPADE, -1, -1, 0
					snd SOUNDLIST_GET3
					noteadd lang("@GRアイテム：" + itemname(ci, inv(INV_ITEM_NUM, ci)) + "を戦利品に加えた！", "@GR" + itemname(ci, inv(INV_ITEM_NUM, ci)) + " has been added to your loot list!")
				}
				else {
					flt
					itemcreate -1, ITEM_ID_SLASH_JOKER, -1, -1, 0
					snd SOUNDLIST_GET3
					noteadd lang("@GRアイテム：" + itemname(ci, inv(INV_ITEM_NUM, ci)) + "を戦利品に加えた！", "@GR" + itemname(ci, inv(INV_ITEM_NUM, ci)) + " has been added to your loot list!")
				}
			}
			if ( keihin >= 29 ) {
				flt
				itemcreate -1, ITEM_ID_STARFIBER, -1, -1, 0
				snd SOUNDLIST_GET3
				noteadd lang("@GRアイテム：" + itemname(ci, inv(INV_ITEM_NUM, ci)) + "を戦利品に加えた！", "@GR" + itemname(ci, inv(INV_ITEM_NUM, ci)) + " has been added to your loot list!")
			}
			if ( keihin >= 31 ) {
				flt
				itemcreate -1, ITEM_ID_CRAFT_REPAIR_KIT, -1, -1, 0
				snd SOUNDLIST_GET3
				noteadd lang("@GRアイテム：" + itemname(ci, inv(INV_ITEM_NUM, ci)) + "を戦利品に加えた！", "@GR" + itemname(ci, inv(INV_ITEM_NUM, ci)) + " has been added to your loot list!")
			}
			if ( keihin >= 33 ) {
				if ( gdata(GDATA_FLAG_CASINO) == 2 ) {
					gdata(GDATA_FLAG_CASINO) = 3
					flt
					itemcreate -1, ITEM_ID_SUPER_LURE, -1, -1, 0
					snd SOUNDLIST_GET3
					noteadd lang("@GRアイテム：" + itemname(ci, inv(INV_ITEM_NUM, ci)) + "を戦利品に加えた！", "@GR" + itemname(ci, inv(INV_ITEM_NUM, ci)) + " has been added to your loot list!")
				}
				else {
					flt
					itemcreate -1, ITEM_ID_POTION_SOMA, -1, -1, 0
					snd SOUNDLIST_GET3
					noteadd lang("@GRアイテム：" + itemname(ci, inv(INV_ITEM_NUM, ci)) + "を戦利品に加えた！", "@GR" + itemname(ci, inv(INV_ITEM_NUM, ci)) + " has been added to your loot list!")
				}
			}
			if ( keihin >= 35 ) {
				if ( gdata(GDATA_FLAG_CASINO) == 3 ) {
					gdata(GDATA_FLAG_CASINO) = 4
					flt
					itemcreate -1, ITEM_ID_SKELETON_KEY, -1, -1, 0
					snd SOUNDLIST_GET3
					noteadd lang("@GRアイテム：" + itemname(ci, inv(INV_ITEM_NUM, ci)) + "を戦利品に加えた！", "@GR" + itemname(ci, inv(INV_ITEM_NUM, ci)) + " has been added to your loot list!")
				}
				else {
					flt
					itemcreate -1, ITEM_ID_POTION_NEKTAR, -1, -1, 0
					snd SOUNDLIST_GET3
					noteadd lang("@GRアイテム：" + itemname(ci, inv(INV_ITEM_NUM, ci)) + "を戦利品に加えた！", "@GR" + itemname(ci, inv(INV_ITEM_NUM, ci)) + " has been added to your loot list!")
				}
			}
		}
		chatList 0, lang("戻る", "Great.")
		chatesc = 0
		txtadvmsgfix = 0
		txtadvscreenupdate = 1
		gosub *com_txtadv_loop
		winrow = 0
	}
	goto *adv_casinoBlackJack

/********** JAMES CUSTOM - SLOTS - BEGINNING **********/ // Why not?
*adv_casinoSlots
	ikasama = 0
	atxinit
	noteadd lang("ブラックジャックは、カードの合計を21に近づけるゲームです。", "In Slots, stop the spinning at right time to connect patterns.")
	noteadd lang("チップが多いほど、景品の質があがります。", "How many tips would you like to bet?")
	atxinfon(1) = lang("カジノチップ残り " + mat(1) + "枚\n", "Casino chips left: " + mat(1) + "\n")
	atxinfon(2) = ""
	atxinfon(3) = ""
	if ( mat(1) <= 0 ) {
		noteadd ""
		noteadd lang("お客様はチップをもっていません。", "Sorry sir, you don't seem to have casino chips.")
	}
	if ( mat(1) >= 1 ) {
		chatList 1, lang("" + 1 + "枚賭ける", "Use " + 1 + " chips.")
	}
	chatList 0, lang("やめる", "I quit.")
	chatesc = 0
	txtadvmsgfix = 0
	txtadvscreenupdate = 1
	gosub *com_txtadv_loop
	if ( rtval == 0 ) {
		goto *adv_casinoCard
	}
	stake = rtval
	autosave = 1 * (gdata(GDATA_AREA) != AREA_SHOW_HOUSE)
	// Init roulette
	mat(1) -= stake
	atxinit
	slotcount@txtadv = 30
	dim slotarray@txtadv, slotcount@txtadv * 3
	repeat slotcount@txtadv * 3
		slotarray@txtadv(cnt) = (rnd(4) + 7) \ 8 // 7, 0, 1, 2
	loop
	repeat 3
		cnt2 = cnt
		repeat 5
			slotarray@txtadv(rnd(slotcount@txtadv) + cnt2 * 30) = 3
		loop
		repeat 3
			slotarray@txtadv(rnd(slotcount@txtadv) + cnt2 * 30) = 6
		loop
		repeat 2
			slotarray@txtadv(rnd(slotcount@txtadv) + cnt2 * 30) = 4
		loop
		repeat 1
			slotarray@txtadv(rnd(slotcount@txtadv) + cnt2 * 30) = 5
		loop
	loop
	slotindex@txtadv = 0
	slotresult@txtadv = -1, -1, -1, -1, -1, -1, -1, -1, -1
	slotoffset@txtadv = rnd(10000), rnd(10000), rnd(10000)
	// Init cheat, and check if player can cheat.
	sandevistan@txtadv = 0
	sandevistanci@txtadv = -1
	sandevistandur@txtadv = 5000 / 3
	repeat MAX_EQUIP_SLOT_TOTAL, CDATA_STARTING_EQUIP_SLOTS
		if ( sandevistanci@txtadv >= 0 ) { break }
		if ( cdata(cnt, CHARA_PLAYER) == 0 ) { continue }

		ci = cdata(cnt, CHARA_PLAYER) \ EXT_EQUIP_SLOTS - 1
		if ( ci < 0 | ci >= MAX_INV_DATA ) { continue }
		repeat RANGE_INV_ITEM_ENCHANT
			if ( inv(STARTING_INV_ITEM_ENCHANT + cnt * 2, ci) == 0 ) { break }

			enc = inv(STARTING_INV_ITEM_ENCHANT + cnt * 2, ci)
			if ( enc == ENCHANT_TIME_STOP ) {
				sandevistanci@txtadv = ci
				break
			}
		loop
	loop
	if ( sandevistanci@txtadv >= MAX_INV_DATA ) { sandevistanci@txtadv = -1 }
	// Init UI - draw the options but don't refresh them.
	noteadd lang("何をしよう？", "Wheel of Fortune")
	chatList 1, lang("探索", "Pull")
	if ( sandevistanci@txtadv >= 0 ) {
		chatList 2, lang("探索", "(Cheat) Slow time using " + itemname(sandevistanci@txtadv) + "!")
	}
	redraw 0
	x = 170, 400
	y = noteinfo(0) * 20 + 120 + txtadvmsgfix + 16, 20 * listmax
	gmode 0
	pos x, y
	gcopy 2, x, y, x(1), y(1)
	gmode 2
	font lang(cfg_font1, cfg_font2), 14 - en * 2, 0
	cs_listbk
	repeat pagesize
		p = pagesize * page + cnt
		if ( p >= listmax ) {
			break
		}
		i = list(0, p)
		display_key 170, noteinfo(0) * 20 + 120 + txtadvmsgfix + 16 + cnt * 20, cnt
		s = listn(0, p)
		cs_list s, 170 + 30, noteinfo(0) * 20 + 120 + txtadvmsgfix + 16 + cnt * 20, 19, 0, 2
	loop
	if ( keyrange != 0 ) {
		cs_bk = cs
	}
	redraw 1
	*adv_casinoSlots_WHILE
	// Render the wheel
	ima = (int(gettime(7)) + int(gettime(6)) * 1000 + int(gettime(5)) * 1000 * 60 + int(gettime(4)) * 1000 * 60 * 60)
	if ( sandevistan@txtadv != 0 ) {
		if ( ima > sandevistan@txtadv ) {
			sandevistan@txtadv = 0
		} else {
			ima = (sandevistan@txtadv - sandevistandur@txtadv) + (ima - (sandevistan@txtadv - sandevistandur@txtadv)) / 20
		}
	}
	x = 200
	y = 200
	repeat 3
		cnt2 = cnt
		if ( slotresult@txtadv(cnt2 * 3) != -1) {
			repeat 3
				cp@txtadv = slotresult@txtadv(cnt2 * 3 + cnt)
				pos x + 48 * cnt2, y + 48 * (2-cnt)
				gcopy 7, 48 * cp@txtadv, 96, 48, 48
			loop
		} else {
			currentoffset@txtadv = slotoffset@txtadv(cnt2) + ima
			repeat 4
				co@txtadv = (currentoffset@txtadv + 48 * cnt) \ (slotcount@txtadv * 48)
				cp@txtadv = slotarray@txtadv(co@txtadv / 48 + cnt2 * 30)
				cy@txtadv = co@txtadv \ 48
				pos x + 48 * cnt2, y + limit(48 * (3-cnt) - 48 + cy@txtadv, 0, 48 * 3)
				gcopy 7, 48 * cp@txtadv, 96 + (cnt==3) * (48 - cy@txtadv), 48, 48 - (cnt==3) * (48 - cy@txtadv) - (cnt == 0) * cy@txtadv
			loop
		}
	loop
	// Check the keys
	await cfg_wait1
	key_check
	if ( key == "b" & sandevistanci@txtadv >= 0 ) {
		if ( ikasama >= 2 ) {
			sandevistan@txtadv = 0
			atxinit
			noteadd lang("イカサマだ！", "Cheater!")
			txt lang("イカサマが見つかってしまった…", "You are caught cheating...")
			modkarma CHARA_PLAYER, -5
			chatList 0, lang("濡れ衣だ！", "I didn't do it!")
			chatesc = -1
			txtadvmsgfix = 0
			txtadvscreenupdate = 1
			gosub *com_txtadv_loop
			goto *adv_casinoCard
		}
		sandevistan@txtadv = ima + sandevistandur@txtadv
		ikasama++
		key = ""
	}
	if ( key != "" ) {
		if (slotindex@txtadv < 3) {
			co@txtadv = (slotoffset@txtadv(slotindex@txtadv) + ima + 24) \ (slotcount@txtadv * 48)
			slotresult@txtadv(slotindex@txtadv * 3) = slotarray@txtadv(co@txtadv / 48 + slotindex@txtadv * 30), slotarray@txtadv((co@txtadv / 48 + 1)\slotcount@txtadv + slotindex@txtadv * 30), slotarray@txtadv((co@txtadv / 48 + 2)\slotcount@txtadv + slotindex@txtadv * 30)
		}
		slotindex@txtadv++
		if ( slotindex@txtadv >= 4 ) {
			goto *adv_casinoSlots_WHILE_END
		}
	}
	goto *adv_casinoSlots_WHILE
	*adv_casinoSlots_WHILE_END
	atxinit
	checklanes@txtadv = 147, 159, 258, 357, 369
	noteadd lang("チェックする", "Checking the wheel...")
	nothing@txtadv = 0
	repeat 5
		keihin = rnd(15)
		checkresult@txtadv = slotresult@txtadv((checklanes@txtadv(cnt)/100)-1) + 1, slotresult@txtadv((checklanes@txtadv(cnt)/10\10)-1) + 1, slotresult@txtadv((checklanes@txtadv(cnt)\10)-1) + 1, -1, -1, -1, -1, -1
		checkresult@txtadv(3) = (checkresult@txtadv(0) == checkresult@txtadv(1)) * checkresult@txtadv(0)
		checkresult@txtadv(4) = (checkresult@txtadv(1) == checkresult@txtadv(2)) * checkresult@txtadv(1)
		checkresult@txtadv(5) = (checkresult@txtadv(0) == checkresult@txtadv(2)) * checkresult@txtadv(0)
		checkresult@txtadv(6) = (checkresult@txtadv(3) == checkresult@txtadv(4)) * checkresult@txtadv(3) // 3 same
		checkresult@txtadv(7) = (checkresult@txtadv(6) == 0) * (checkresult@txtadv(3) + checkresult@txtadv(4) + checkresult@txtadv(5)) // 2 same
		// dialog("//0:"+checkresult@txtadv(0)+"//1:"+checkresult@txtadv(1)+"//2:"+checkresult@txtadv(2)+"//3:"+checkresult@txtadv(3)+"//4:"+checkresult@txtadv(4)+"//5:"+checkresult@txtadv(5)+"//6:"+checkresult@txtadv(6)+"//7:"+checkresult@txtadv(7))
		if ( checkresult@txtadv(6) == 1 ) { // 3 putits
			noteadd "3 putits!"
			repeat 1
				flt
				flttypemajor = fsetincome(rnd(length(fsetincome)))
				itemcreate -1, ITEM_ID_DUMMY, -1, -1, 0
				if ( inv(INV_ITEM_NUM, ci) == 0 ) {
					continue cnt
				}
			loop
			snd SOUNDLIST_GET3
			noteadd lang("@GRアイテム：" + itemname(ci, inv(INV_ITEM_NUM, ci)) + "を戦利品に加えた！", "@GR" + itemname(ci, inv(INV_ITEM_NUM, ci)) + " has been added to your loot list!")
			continue
		}
		if ( checkresult@txtadv(6) == 2 ) { // 3 dogs, gives you a FURNITURE!
			noteadd "3 dogs!"
			repeat 1
				flt
				flttypemajor = FILTER_FURNITURE
				p@txtadv = ITEM_ID_DUMMY
				if ( rnd(1000) == 0 ) { p@txtadv = 1115 }
				itemcreate -1, p@txtadv, -1, -1, 0
				if ( inv(INV_ITEM_NUM, ci) == 0 ) {
					continue cnt
				}
			loop
			snd SOUNDLIST_GET3
			noteadd lang("@GRアイテム：" + itemname(ci, inv(INV_ITEM_NUM, ci)) + "を戦利品に加えた！", "@GR" + itemname(ci, inv(INV_ITEM_NUM, ci)) + " has been added to your loot list!")
			continue
		}
		if ( checkresult@txtadv(6) == 3 ) { // 3 breads, give a herb.
			noteadd "3 breads!"
			if (rnd(1)==0) { p@txtadv = ITEM_ID_STOMAFILLIA }
			if (rnd(2)==0) { p@txtadv = ITEM_ID_CURARIA }
			if (rnd(3)==0) {
				if (rnd(1)==0) { p@txtadv = ITEM_ID_MORGIA }
				if (rnd(2)==0) { p@txtadv = ITEM_ID_MAREILON }
				if (rnd(3)==0) { p@txtadv = ITEM_ID_SPENSEWEED }
				if (rnd(4)==0) { p@txtadv = ITEM_ID_ALRAUNIA }
			}
			flt
			itemcreate -1, p@txtadv, -1, -1, 0
			snd SOUNDLIST_GET3
			noteadd lang("@GRアイテム：" + itemname(ci, inv(INV_ITEM_NUM, ci)) + "を戦利品に加えた！", "@GR" + itemname(ci, inv(INV_ITEM_NUM, ci)) + " has been added to your loot list!")
			continue
		}
		if ( checkresult@txtadv(6) == 4 ) { // 3 cats, minor gift
			noteadd "3 cats!"
			flt
			itemcreate -1, isetgiftminor(rnd(length(isetgiftminor))), -1, -1, 1
			snd SOUNDLIST_GET3
			noteadd lang("@GRアイテム：" + itemname(ci, inv(INV_ITEM_NUM, ci)) + "を戦利品に加えた！", "@GR" + itemname(ci, inv(INV_ITEM_NUM, ci)) + " has been added to your loot list!")
			continue
		}
		if ( checkresult@txtadv(6) == 7 ) { // 3 bethels, gives major gift
			noteadd "3 bethels!"
			flt
			itemcreate -1, isetgiftmajor(rnd(length(isetgiftmajor))), -1, -1, 1
			snd SOUNDLIST_GET3
			noteadd lang("@GRアイテム：" + itemname(ci, inv(INV_ITEM_NUM, ci)) + "を戦利品に加えた！", "@GR" + itemname(ci, inv(INV_ITEM_NUM, ci)) + " has been added to your loot list!")
			continue
		}
		if ( checkresult@txtadv(6) == 5 ) { // 3 larnneires, gives grand gift
			noteadd "3 larnneires!"
			flt
			itemcreate -1, isetgiftgrand(rnd(length(isetgiftgrand))), -1, -1, 1
			snd SOUNDLIST_GET3
			noteadd lang("@GRアイテム：" + itemname(ci, inv(INV_ITEM_NUM, ci)) + "を戦利品に加えた！", "@GR" + itemname(ci, inv(INV_ITEM_NUM, ci)) + " has been added to your loot list!")
			continue
		}
		if ( checkresult@txtadv(6) == 6 ) { // 3 ehekatls, artifcat
			noteadd "3 ehekatls!"
			snd SOUNDLIST_OFFER2
			kaiwa = rnd(22)
			if ( kaiwa == 0 ) { id = ITEM_ID_GO_RENGE }
			if ( kaiwa == 1 ) { id = ITEM_ID_EPECLAIR }
			if ( kaiwa == 2 ) { id = ITEM_ID_GOUTEN }
			if ( kaiwa == 3 ) { id = ITEM_ID_DGT_101 }
			if ( kaiwa == 4 ) { id = ITEM_ID_NUMENIUS }
			if ( kaiwa == 5 ) { id = ITEM_ID_ENGOKU }
			if ( kaiwa == 6 ) { id = ITEM_ID_POWER_TRIANGLE }
			if ( kaiwa == 7 ) { id = ITEM_ID_CROSS_RAIL }
			if ( kaiwa == 8 ) { id = ITEM_ID_NAZUNAS_SOX }
			if ( kaiwa == 9 ) { id = ITEM_ID_SCORPIERCE }
			if ( kaiwa == 10 ) { id = ITEM_ID_YOMI_TO }
			if ( kaiwa == 11 ) { id = ITEM_ID_RINKHALS }
			if ( kaiwa == 12 ) { id = ITEM_ID_LINGS_BOW }
			if ( kaiwa == 13 ) { id = ITEM_ID_BEETLE_CYBORG }
			if ( kaiwa == 14 ) { id = ITEM_ID_ASSAULT_GOLD }
			if ( kaiwa == 15 ) { id = ITEM_ID_DOKURO_MIKADUKI }
			if ( kaiwa == 16 ) { id = ITEM_ID_BLOODY_TEARS }
			if ( kaiwa == 17 ) { id = ITEM_ID_NIGHTMARE }
			if ( kaiwa == 18 ) { id = ITEM_ID_GEMINI }
			if ( kaiwa == 19 ) { id = ITEM_ID_HIRYU_TO }
			if ( kaiwa == 20 ) { id = ITEM_ID_BAMBNIR }
			if ( kaiwa == 21 ) { id = ITEM_ID_ZANTETSUKEN }
			flt
			itemcreate -1, id, cdata(CDATA_X, CHARA_PLAYER), cdata(CDATA_Y, CHARA_PLAYER), 0
			inv(INV_ITEM_STATUS, ci) = ITEM_STATUS_NORMAL
			snd SOUNDLIST_GET3
			noteadd lang("@GRアイテム：" + itemname(ci, inv(INV_ITEM_NUM, ci)) + "を戦利品に加えた！", "@GR" + itemname(ci, inv(INV_ITEM_NUM, ci)) + " has been added to your loot list!")
			continue
		}
		if ( checkresult@txtadv(6) == 8 ) { // 3 crimberries, gives 2 casino chip
			noteadd "3 crimberries!"
			p@txtadv = 1
			matgetmain p@txtadv, 2, 5
			noteadd "@BL" + locvar_matgetmain_s
			continue
		}
		if ( checkresult@txtadv(7) == 3 ) { // 2 breads, give some food.
			noteadd "2 breads!"
			if (rnd(1)==0) { p@txtadv = ITEM_ID_POP_CORN }
			if (rnd(2)==0) { p@txtadv = ITEM_ID_FRIED_POTATO }
			if (rnd(3)==0) { p@txtadv = ITEM_ID_CYBER_SNACK }
			if (rnd(20)==0){
				if (rnd(1)==0) { p@txtadv = ITEM_ID_YITH_YAKI }
				if (rnd(2)==0) { p@txtadv = ITEM_ID_MOCHI }
				if (rnd(10)==0) { p@txtadv = ITEM_ID_KAGAMI_MOCHI }
			}
			flt
			itemcreate -1, p@txtadv, -1, -1, 0
			snd SOUNDLIST_GET3
			noteadd lang("@GRアイテム：" + itemname(ci, inv(INV_ITEM_NUM, ci)) + "を戦利品に加えた！", "@GR" + itemname(ci, inv(INV_ITEM_NUM, ci)) + " has been added to your loot list!")
			continue
		}
		if ( checkresult@txtadv(7) == 5 ) { // 2 larnneires, gives something blessed
			noteadd "2 larnneires!"
			repeat 1
				i = limit(keihin/7+2 , 2, 4)
				flt calcobjlv(rnd(keihin + 1) + keihin), i
				flttypemajor = fsetwear(rnd(length(fsetwear)))
				itemcreate -1, ITEM_ID_DUMMY, -1, -1, 0
				if ( inv(INV_ITEM_NUM, ci) == 0 ) {
					continue cnt
				}
			loop
			inv(INV_ITEM_STATUS, ci) = ITEM_STATUS_BLESSED
			snd SOUNDLIST_GET3
			noteadd lang("@GRアイテム：" + itemname(ci, inv(INV_ITEM_NUM, ci)) + "を戦利品に加えた！", "@GR" + itemname(ci, inv(INV_ITEM_NUM, ci)) + " has been added to your loot list!")
			continue
		}
		if ( checkresult@txtadv(7) == 7 ) { // 2 bethels, gives something cursed.
			noteadd "2 bethels!"
			repeat 1
				i = limit(keihin/7+2 , 2, 4)
				flt calcobjlv(rnd(keihin + 1) + keihin), i
				flttypemajor = fsetwear(rnd(length(fsetwear)))
				itemcreate -1, ITEM_ID_DUMMY, -1, -1, 0
				if ( inv(INV_ITEM_NUM, ci) == 0 ) {
					continue cnt
				}
			loop
			inv(INV_ITEM_STATUS, ci) = ITEM_STATUS_CURSED
			if (rnd(3) == 0) { inv(INV_ITEM_STATUS, ci) = ITEM_STATUS_DOOMED }
			snd SOUNDLIST_GET3
			noteadd lang("@GRアイテム：" + itemname(ci, inv(INV_ITEM_NUM, ci)) + "を戦利品に加えた！", "@GR" + itemname(ci, inv(INV_ITEM_NUM, ci)) + " has been added to your loot list!")
			continue
		}
		if ( checkresult@txtadv(7) == 8 ) { // 2 crimberries, give 1 casino chip
			noteadd "2 crimberries!"
			p@txtadv = 1
			matgetmain p@txtadv, 1, 5
			noteadd "@BL" + locvar_matgetmain_s
			continue
		}
		nothing@txtadv++
	loop
	if ( nothing@txtadv == 5 ) {
		noteadd lang("何もない...", "Nothing... scut!")
	}
	chatList 0, lang("戻る", "Great.")
	chatesc = 0
	txtadvmsgfix = 0
	txtadvscreenupdate = 1
	gosub *com_txtadv_loop
	goto *adv_casinoSlots

/********** JAMES CUSTOM - SLOTS - ENDING **********/ 