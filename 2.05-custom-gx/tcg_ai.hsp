*ai_evaluate
	emax@tcg = 0
	repeat clistmax@tcg(ch@tcg)
		if ( aiblock@tcg ) {
			break
		}
		c@tcg = clist@tcg(cnt, ch@tcg)
		p@tcg = 10
		if ( card@tcg(TCG_CARD_REF_TYPE, c@tcg) == TCG_TYPE_CREATURE ) {
			p@tcg = 20
			p@tcg -= abs(cpdata@tcg(TCG_PLAYER_MAX_MANA, cp@tcg) - card@tcg(TCG_CARD_REF_COST, c@tcg))
		}
		// Legendary cards are played first and discarded last.
		if ( card@tcg(TCG_CARD_REF_DOMAIN, c@tcg) == 5 ) { p@tcg += 10 }
		if ( effref@tcg(TCG_SKILL_AI_PREFERENCE, card@tcg(TCG_CARD_EFFECT, c@tcg)) == TCG_SKILL_AI_HOLD ) { p@tcg -= getholdersum(1) / 2 }
		if ( effref@tcg(TCG_SKILL_AI_PREFERENCE, card@tcg(TCG_CARD_EFFECT, c@tcg)) == TCG_SKILL_AI_DRAW ) { p@tcg += 2 * (5 - getholdersum(1)) }
		if ( effref@tcg(TCG_SKILL_AI_PREFERENCE, card@tcg(TCG_CARD_EFFECT, c@tcg)) == TCG_SKILL_AI_SAC ) { p@tcg -= 2 * (8 - cpdata@tcg(TCG_PLAYER_MAX_MANA, cp@tcg)) }
		elist@tcg(TCG_EVAL, emax@tcg) = p@tcg, c@tcg, TCG_ACTION_SUMMON
		emax@tcg++
	loop
	if ( aiblock@tcg == 0 ) {
		repeat clistmax@tcg(cl@tcg)
			c@tcg = clist@tcg(cnt, cl@tcg)
			if ( cardcandeclareattack(c@tcg) ) {
				p@tcg = 20 + rnd(10)
				// These cards attack later
				if ( cdbit(TCG_BIT_TRAMPLE, c@tcg) ) { p@tcg -= 10 }
				if ( cdbit(TCG_BIT_FLYING, c@tcg) ) { p@tcg -= 5 }
				if ( cdbit(TCG_BIT_INTIMIDATE, c@tcg) ) { p@tcg -= 5 }
				// Don't attack with 0 attack cards. (Occasionally to annoy you)
				if ( card@tcg(TCG_CARD_ATTACK, c@tcg) <= 0 & rnd(40) > 0 ) { p@tcg = -100 }
				// Do not attack using cards needed to be kept on field
				if ( effref@tcg(TCG_SKILL_AI_PREFERENCE, card@tcg(TCG_CARD_EFFECT, c@tcg)) == TCG_SKILL_AI_KEEPALIVE ) {
					repeat clistmax@tcg(tl@tcg)
						if ( cardcanblock(clist@tcg(cnt, tl@tcg))) { 
							cardcankill@tcg = card@tcg(TCG_CARD_ATTACK, clist@tcg(cnt, tl@tcg)) + 50 * cdbit(TCG_BIT_DEATHTOUCH, clist@tcg(cnt, tl@tcg)) >= card@tcg(TCG_CARD_HP, c@tcg) + 100 * cdbit(TCG_BIT_ARMOR, c@tcg) + 100 * (card@tcg(TCG_CARD_HP, c@tcg) > 2) * cdbit(TCG_BIT_RIDER, c@tcg)
							if ( cardcankill@tcg ) {
								p@tcg = -101
							}
						}
					loop 
				}
				// Deathtouch card attack with some chance to annoy you.
				if ( cdbit(TCG_BIT_DEATHTOUCH, c@tcg) ) { p@tcg = 0 + 40 * rnd(2) }

				// Below line visualized defend weight, enable if you want to debug
				// efllistadd 1 + (p@tcg > 0), p@tcg, card@tcg(TCG_CARD_X, c@tcg), card@tcg(TCG_CARD_Y, c@tcg), card@tcg(TCG_CARD_X, c@tcg), card@tcg(TCG_CARD_Y, c@tcg)

				elist@tcg(TCG_EVAL, emax@tcg) = p@tcg, c@tcg, TCG_ACTION_ATTACKING
				emax@tcg++
			}
		loop
	}
	else {
		repeat clistmax@tcg(cl@tcg)
			c@tcg = clist@tcg(cnt, cl@tcg)
			if ( cardcanblock(c@tcg, attackingcard@tcg) ) {
				// Initial weight being HP - attack diff. block with highest possible HP and lowest possible attack diff.
				p@tcg = limit(card@tcg(TCG_CARD_HP, c@tcg) - limit(card@tcg(TCG_CARD_ATTACK, c@tcg) - card@tcg(TCG_CARD_ATTACK, attackingcard@tcg), -10, 10) * 3, -20, 20)
				// Surplus basic merit flying/lifelink:
				if ( cdbit(TCG_BIT_LIFELINK, attackingcard@tcg) ) { p@tcg -= card@tcg(TCG_CARD_HP, attackingcard@tcg) }
				if ( cdbit(TCG_BIT_TRAMPLE, attackingcard@tcg) ) { p@tcg += card@tcg(TCG_CARD_HP, c@tcg) }
				if ( cdbit(TCG_BIT_FLYING, attackingcard@tcg) ) { p@tcg += 10 }
				if ( cdbit(TCG_BIT_INTIMIDATE, attackingcard@tcg) ) { p@tcg += 20 }
				// Get basic combat predictions
				cardsurvive@tcg = card@tcg(TCG_CARD_ATTACK, attackingcard@tcg) + 50 * cdbit(TCG_BIT_DEATHTOUCH, attackingcard@tcg) < card@tcg(TCG_CARD_HP, c@tcg) + 100 * cdbit(TCG_BIT_ARMOR, c@tcg) + 100 * (card@tcg(TCG_CARD_HP, c@tcg) > 2) * cdbit(TCG_BIT_RIDER, c@tcg)
				cardcankill@tcg = card@tcg(TCG_CARD_ATTACK, c@tcg) + 50 * cdbit(TCG_BIT_DEATHTOUCH, c@tcg) >= card@tcg(TCG_CARD_HP, attackingcard@tcg) + 100 * cdbit(TCG_BIT_ARMOR, attackingcard@tcg) + 100 * (card@tcg(TCG_CARD_HP, attackingcard@tcg) > 2) * cdbit(TCG_BIT_RIDER, attackingcard@tcg)
				cardvantage@tcg = (cdbit(TCG_BIT_FIRST_STRIKE, c@tcg) | cdbit(TCG_BIT_DOUBLE_STRIKE, c@tcg)) - (cdbit(TCG_BIT_FIRST_STRIKE, attackingcard@tcg) | cdbit(TCG_BIT_DOUBLE_STRIKE, attackingcard@tcg))
				// Basic card advantage calcs
				if ( cardvantage@tcg < 0 & cardsurvive@tcg == 0 ) { cardcankill@tcg = 0 }
				if ( cardsurvive@tcg == 0 & cardcankill@tcg == 0 ) { p@tcg -= 2 }
				if ( cardsurvive@tcg ) { p@tcg += 10 }
				if ( cardcankill@tcg ) { p@tcg += 10 }
				if ( cardcankill@tcg & cardvantage@tcg ) { p@tcg += 10 }
				if ( cardsurvive@tcg & cdbit(TCG_BIT_REGENERATION, c@tcg) ) { p@tcg += 5 }
				if ( cardsurvive@tcg & cdbit(TCG_BIT_REGENERATION, c@tcg) & cdbit(TCG_BIT_VIGILANCE, c@tcg) ) { p@tcg += 5 }
				// Block with zombie/fodder with higher prio
				if ( card@tcg(TCG_CARD_ATTACK, c@tcg) == 0 ) { p@tcg += 5 }
				if ( card@tcg(TCG_CARD_EFFECT, c@tcg) == TCG_EFF_RETURNTOHANDWHENKILLED ) { p@tcg += 10 }
				if ( effref@tcg(TCG_SKILL_EFF, card@tcg(TCG_CARD_EFFECT, c@tcg)) == TCG_SKILL_EFF_RESURRECT ) { p@tcg += 10 }
				// Do not defend using cards needed to be kept on field
				if ( effref@tcg(TCG_SKILL_AI_PREFERENCE, card@tcg(TCG_CARD_EFFECT, c@tcg)) == TCG_SKILL_AI_KEEPALIVE & cardsurvive@tcg == 0 ) { p@tcg = -101 }
				// Deathtouch card defend with some chance to annoy you.
				if ( cdbit(TCG_BIT_DEATHTOUCH, c@tcg) & cardvantage@tcg >= 0 ) { p@tcg = 10 + (-40) * (cardsurvive@tcg == 0) + 40 * rnd(2) }
				// Block attack with splitting creatures for profit.
				if ( cardsurvive@tcg & cdbit(TCG_BIT_SPLIT, c@tcg) ) { p@tcg = 101 }
				// Preferably, don't block first-attack cards.
				if ( cardvantage@tcg < 0 & cardsurvive@tcg == 0 ) { p@tcg = -102 }
				// Don't block 0 attack cards.
				if ( card@tcg(TCG_CARD_ATTACK, attackingcard@tcg) <= 0 & rnd(100) > 0 ) { p@tcg = -103 }
				// Force a block if life in danger
				if ( card@tcg(TCG_CARD_ATTACK, attackingcard@tcg) >= cpdata@tcg(TCG_PLAYER_LIFE, cp@tcg) - rnd(5) - rnd(5) ) {
					p@tcg = 102
				}
				// Below line visualized defend weight, enable if you want to debug
				// efllistadd 1 + (p@tcg > 0), p@tcg, card@tcg(TCG_CARD_X, c@tcg), card@tcg(TCG_CARD_Y, c@tcg), card@tcg(TCG_CARD_X, c@tcg), card@tcg(TCG_CARD_Y, c@tcg)
				if ( p@tcg >= 100 | p@tcg <= -100 ) { p@tcg += 5 - rnd(10) }
				
				elist@tcg(TCG_EVAL, emax@tcg) = p@tcg, c@tcg, TCG_ACTION_DEFENDING
				emax@tcg++
			}
		loop
	}
	if ( emax@tcg == 0 ) {
		return
	}
	repeat
		f@tcg = 0
		repeat emax@tcg - 1
			if ( elist@tcg(TCG_EVAL_PROB, cnt) < elist@tcg(TCG_EVAL_PROB, cnt + 1) ) {
				cnt2@tcg = cnt
				repeat 3
					p@tcg(cnt) = elist@tcg(cnt, cnt2@tcg + 1)
					elist@tcg(cnt, cnt2@tcg + 1) = elist@tcg(cnt, cnt2@tcg)
					elist@tcg(cnt, cnt2@tcg) = p@tcg(cnt)
				loop
				f@tcg = 1
				break
			}
		loop
		if ( f@tcg == 0 ) {
			break
		}
	loop
	return

*ai_sac
	if ( sac@tcg > 0 ) {
		return 0
	}
	if ( getholdersum(cp@tcg) <= cpdata@tcg(TCG_PLAYER_MAX_MANA, cp@tcg) / 4 + 1 ) {
		if ( rnd(4) ) {
			return 0
		}
	}
	f@tcg = 0
	p@tcg(1) = 50
	repeat emax@tcg
		c@tcg = elist@tcg(TCG_EVAL_TARGET, cnt)
		p@tcg = elist@tcg(TCG_EVAL_PROB, cnt)
		if ( card@tcg(TCG_CARD_LOCATION, c@tcg) != TCG_LOCATION_HAND ) {
			continue
		}
		if ( effref@tcg(TCG_SKILL_AI_PREFERENCE, card@tcg(TCG_CARD_EFFECT, c@tcg)) == TCG_SKILL_AI_HOLD ) { p@tcg += 50 }
		if ( effref@tcg(TCG_SKILL_AI_PREFERENCE, card@tcg(TCG_CARD_EFFECT, c@tcg)) == TCG_SKILL_AI_SAC ) { p@tcg -= 20 }
		if ( card@tcg(TCG_CARD_REF_DOMAIN, c@tcg) == 5 ) { p@tcg += 20 }
		if ( p@tcg < p@tcg(1) ) {
			p@tcg(1) = p@tcg
			cc@tcg = c@tcg
			f@tcg = 1
		}
	loop
	if ( f@tcg ) {
		saccard cc@tcg, cp@tcg
		tcgdraw
		return 1
	}
	return 0

*card_ai
	spellused@tcg = 0
	aievacycle@tcg = 0
	*card_ai_RETRY
	aievacycle@tcg++
	if ( debug ) {
		await
		getkey a@tcg, 32
		if ( a@tcg ) {
			await 500
		}
	}
	else {
		stick a@tcg
		if ( a@tcg ) {
			promptAdd lang("降参する", "Escape?"), "a"
			promptAdd lang("降参する", "No"), "b"
			val = basex@tcg + 420, basey@tcg + 230, 200, 1
			gosub *prompt_key@
			if ( rtval == 0 ) {
				gameresult@tcg = -2
				cpdata@tcg(TCG_PLAYER_LIFE, 0) = 0
				return
			} else {
				gosub *ai_evaluate
				goto *card_ai_WHILE1
			}
		}
	}
	gosub *card_resolve_ongoing
	gosub *ai_evaluate
	ec@tcg = 0
	*card_ai_WHILE1
	if ( gameover() ) {
		goto *card_ai_WEND1
	}
	if ( ec@tcg >= emax@tcg ) {
		goto *card_ai_WEND1
	}
	if ( elist@tcg(TCG_EVAL_PROB, ec@tcg) <= 0 ) {
		goto *card_ai_WEND1
	}
	cc@tcg = elist@tcg(TCG_EVAL_TARGET, ec@tcg)
	if ( elist@tcg(TCG_EVAL_ACTION, ec@tcg) == TCG_ACTION_SUMMON ) {
		if ( card@tcg(TCG_CARD_COST, cc@tcg) <= cpdata@tcg(TCG_PLAYER_MANA, cp@tcg) ) {
			putcard cc@tcg, cp@tcg
			if ( stat == -1 ) {
				goto *card_ai_CARDSKIP
			}
			if ( gameover() ) {
				goto *card_ai_WEND1
			}
			if ( card@tcg(TCG_CARD_REF_TYPE, cc@tcg) == TCG_TYPE_SPELL ) {
				cardstack@tcg(TCG_STACK, stack@tcg) = cc@tcg, TCG_ACTION_INVOKING, cp@tcg
				stack@tcg++
				if ( chainmode@tcg ) {
					chaincontinue@tcg = 1
					spellused@tcg = 1
					goto *card_ai_WEND1
				}
				actionproc
			}
			goto *card_ai
		}
		else {
			gosub *ai_sac
			if ( stat == 1 ) {
				goto *card_ai
			}
		}
	}
	if ( elist@tcg(TCG_EVAL_ACTION, ec@tcg) == TCG_ACTION_ATTACKING ) {
		cardstack@tcg(TCG_STACK, stack@tcg) = cc@tcg, TCG_ACTION_ATTACKING, cp@tcg
		stack@tcg++
		actionproc
	}
	if ( elist@tcg(TCG_EVAL_ACTION, ec@tcg) == TCG_ACTION_DEFENDING ) {
		attacktarget@tcg = cc@tcg
		goto *card_ai_WEND1
	}
	*card_ai_CARDSKIP
	ec@tcg++
	goto *card_ai_WHILE1
	*card_ai_WEND1
	if ( aiblock@tcg == 0 ) {
		if ( sac@tcg == 0 ) {
			gosub *ai_sac
			if ( stat == 1 ) {
				goto *card_ai
			}
		}
		if ( aievacycle@tcg < 3 ) {
			goto *card_ai_RETRY
		}
	}
	if ( gameover() ) {
		return
	}
	return

